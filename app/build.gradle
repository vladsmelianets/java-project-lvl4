plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'jacoco'
    id 'io.ebean' version '13.9.2'
    id "com.github.ben-manes.versions" version '0.42.0'
}

group 'hexlet.code'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

//Необходимо для того, что бы сгенерированные Q-классы тоже включались в сборку приложения
sourceSets.main.java.srcDirs = ['src/main/java', 'build/generated/sources/annotationProcessor/java/main']

dependencies {
    // doesn't work with javalin higher than 4.6.4
    implementation 'io.javalin:javalin:4.6.4'
    implementation 'org.slf4j:slf4j-simple:2.0.0'

    implementation 'com.h2database:h2:2.1.214'
    implementation 'org.postgresql:postgresql:42.5.0'

    // doesn't work with thymeleaf higher than 3.0.12.RELEASE
    implementation 'org.thymeleaf:thymeleaf:3.0.12.RELEASE'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.1.0'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'


    implementation 'org.webjars:bootstrap:5.2.0'

    implementation 'io.ebean:ebean:13.9.2'
    implementation 'io.ebean:ebean-querybean:13.9.2'
    implementation 'io.ebean:ebean-ddl-generator:13.9.2'
    implementation 'io.ebean:ebean-annotation:8.2'
    implementation 'io.ebean:ebean-migration:13.6.2'
    annotationProcessor 'io.ebean:querybean-generator:13.9.2'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'com.konghq:unirest-java:3.13.6'
    implementation 'org.jsoup:jsoup:1.15.3'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'io.ebean:ebean-test:12.15.1'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'

}

application {
    mainClass = 'hexlet.code.App'
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

testlogger {
    theme 'plain'
    showStandardStreams true
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

// modifying checkstyleMain to exclude generated sources from the checks
checkstyleMain {
    source = fileTree('src/main/java')
}
checkstyleMain.exclude('build/')


task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.MigrationGenerator'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean
